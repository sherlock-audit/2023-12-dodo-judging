Decent Gauze Goldfish

medium

# `D3UserQuota::getTierQuota` is not working as expected

## Summary
The `getTierQuota()` function returns more quota than it should do

## Vulnerability Detail
Users can deposit tokens based on quota limits. These limits might be a global quota for that token or it can be a tiered quota.

The tiered quota is determined based on how many [`_vTOKEN`](https://github.com/sherlock-audit/2023-12-dodo/blob/main/dodo-v3/contracts/DODOV3MM/D3Vault/periphery/D3UserQuota.sol#L16C1-L17C29) a user holds.

```solidity
    // only user who holds vToken can deposit money
    address public _vTOKEN_;
```

Since there are no documentation regarding how exactly tiered quota should work, we can assume developer comments are the source of truth.

[https://github.com/sherlock-audit/2023-12-dodo/blob/main/dodo-v3/contracts/DODOV3MM/D3Vault/periphery/D3UserQuota.sol#L57C1-L80C6](https://github.com/sherlock-audit/2023-12-dodo/blob/main/dodo-v3/contracts/DODOV3MM/D3Vault/periphery/D3UserQuota.sol#L57C1-L80C6)

```solidity
    /// @notice Set the amount of tokens held and their corresponding quotas
    /// @notice for example, tiers = [100, 200, 300, 400], amounts = [1000, 4000, 6000, 10000]
    /// @notice user who holds 100 vToken, can deposit 1000 token
    /// @notice user who holds 200 vToken, can deposit 4000 token
    /// @notice user who holds 300 vToken, can deposit 6000 token
    /// @notice user who holds 400 vToken, can deposit 10000 token
```

And here is the `getTierQuota()` function:

```solidity
 /// @notice Get the user quota based on tier
    function getTierQuota(address user, address token) public view returns (uint256 quota) {
        uint256 vTokenBalance = IERC20(_vTOKEN_).balanceOf(user);
        uint256[] memory tiers = vTokenTiers[token];
        uint256[] memory amounts = quotaOnTiers[token];
        for (uint256 i = 0; i < tiers.length; i++) {
            if (vTokenBalance < tiers[i]) {
                return quota = amounts[i];
            }
        }
        quota = amounts[amounts.length - 1];
    }
```

This function is not working as described above.

This function works like "user who holds **up to** 100 `vToken`, can deposit 1000 token". Even a user that can not have 1st tier amount of `vToken` can still deposit corresponding to the first tier.

From my understanding, the tier amounts are the **required** `vToken` amounts to be able to deposit. If this is not the case and the protocol's intention was indeed to use these tiers as "**up to",** this brings another problem which makes the last tier redundant.

For the same example above:

* For user with 301 tokens (`balance < tiers[3]`) -&gt; `quota = amounts[3]` -&gt; 10000 tokens.
    
* For user with 401 token (above the last tier) -&gt; `quota = amounts[amounts.length - 1]` -&gt; `amounts[3]` -&gt; 10000 tokens again. Having the last tier doesn't change anything.

## Impact
The `getTierQuota()` function returns more quota than it should do

## Code Snippet
https://github.com/sherlock-audit/2023-12-dodo/blob/main/dodo-v3/contracts/DODOV3MM/D3Vault/periphery/D3UserQuota.sol#L57C1-L80C6

## Tool used

Manual Review

## Recommendation
An example snippet can be found below _(Note: It is created with the assumption of these tiered tokens are "required" amount of tokens, not "up to" amounts)._

```solidity
    function getTierQuota(address user, address token) public view returns (uint256 quota) {
        uint256 vTokenBalance = IERC20(_vTOKEN_).balanceOf(user);
        uint256[] memory tiers = vTokenTiers[token];
        uint256[] memory amounts = quotaOnTiers[token];
        
        // If user doesn't even have first tier amount of tokens, quota is 0.
        if (vTokenBalance < tiers[0] {
            return quota = 0;
        }
         
        // Start the loop with the second tier (i = 1)
        for (uint256 i = 1; i < tiers.length; i++) {
            if (vTokenBalance < tiers[i]) {
                // If the balance can't reach the next tier, return the previous tier's quota. --> quota = amounts[i - 1]
                return quota = amounts[i - 1];
            }
        }
        quota = amounts[amounts.length - 1];
    }
```