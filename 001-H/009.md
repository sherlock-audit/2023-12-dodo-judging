Decent Gauze Goldfish

high

# Swap fees are not charged in `D3Trading::sellToken()`

## Summary
`D3Trading::sellToken()` function queries how many tokens will be sent to the user and this is done with `querySellTokens` function. Swap fees are never deducted but the maintainer fee is deducted while returning `receieveToAmount` value in this function.

## Vulnerability Detail
This protocol has two separate fees: Swap fees and maintainer fees. Swap fees are paid by the user and the protocol takes these swap fees as `toToken`.

Users provide exactly how many `fromToken` to sell in `D3Trading::sellToken()` function, and get corresponding amount of `toToken`. The `toToken` amount to receive is queried here with the `D3Trading::querySellTokens` function. Let's check the `sellToken()` function:

[https://github.com/sherlock-audit/2023-12-dodo/blob/main/dodo-v3/contracts/DODOV3MM/D3Pool/D3Trading.sol#L105C3-L107C80](https://github.com/sherlock-audit/2023-12-dodo/blob/main/dodo-v3/contracts/DODOV3MM/D3Pool/D3Trading.sol#L105C3-L107C80)

```solidity
    function sellToken(
        address to,
        address fromToken,
        address toToken,
        uint256 fromAmount,
        uint256 minReceiveAmount,
        bytes calldata data
    ) external virtual poolOngoing nonReentrant returns (uint256) {
        // ...

        (uint256 payFromAmount, uint256 receiveToAmount, uint256 vusdAmount, uint256 swapFee, uint256 mtFee) =
106.-->      querySellTokens(fromToken, toToken, fromAmount); //@audit swapFee is never deducted in here. Only mtFee deducted. The receiveToAmount here is "receiveToAmount - mtFee" not "receiveToAmount - swapFee" 
        require(receiveToAmount >= minReceiveAmount, Errors.MINRES_NOT_ENOUGH);

        //... Transfer and callbacks...

        // record swap
        uint256 toTokenDec = IERC20Metadata(toToken).decimals();
-->     _recordSwap(fromToken, toToken, vusdAmount, Types.parseRealAmount(receiveToAmount + swapFee, toTokenDec)); //@audit recorded toAmount is "receiveToAmount + swapFee"
        require(checkSafe(), Errors.BELOW_IM_RATIO);

        emit Swap(to, fromToken, toToken, payFromAmount, receiveToAmount, swapFee, mtFee, 0);
        return receiveToAmount;
    }
```

This function queries how many `toToken`s to receive with `querySellTokens` function, makes transfers, and records the swap with `_recordSwap` function. I would like to point out that the inputted `toAmount` parameter in the `_recordSwap` is `receiveToAmount + swapFee` (parsed according to decimals).

Now, let's check `querySellTokens` function:  
[https://github.com/sherlock-audit/2023-12-dodo/blob/main/dodo-v3/contracts/DODOV3MM/D3Pool/D3Trading.sol#L201C9-L206C85](https://github.com/sherlock-audit/2023-12-dodo/blob/main/dodo-v3/contracts/DODOV3MM/D3Pool/D3Trading.sol#L201C9-L206C85)

```solidity
    function querySellTokens(
        address fromToken,
        address toToken,
        uint256 fromAmount
    ) public view returns (uint256 payFromAmount, uint256 receiveToAmount, uint256 vusdAmount, uint256 swapFee, uint256 mtFee) {
        // ... skipped for brevity       

        receiveToAmount = receiveToAmount > state.balances[toToken] ? state.balances[toToken] : receiveToAmount;

        uint256 swapFeeRate = D3State.fromTokenMMInfo.swapFeeRate +  D3State.toTokenMMInfo.swapFeeRate;
        swapFee = DecimalMath.mulFloor(receiveToAmount, swapFeeRate);
        uint256 mtFeeRate = D3State.fromTokenMMInfo.mtFeeRate +  D3State.toTokenMMInfo.mtFeeRate;
        mtFee = DecimalMath.mulFloor(receiveToAmount, mtFeeRate);

-->    return (payFromAmount, receiveToAmount - mtFee, vusdAmount, swapFee, mtFee); //@audit receiveToAmount is "receiveToAmount - mtFee", not "receiveToAmount - swapFee"
    }
```

As I mentioned in the beginning, swap fees are calculated based on `toToken`s. This function calculates both `swapFee` and `mtFee`, but only the `mtFee` is deducted from the `receiveToAmount`. The `swapFee` is never deducted. Users are not charged at all.

I would like to point out the previous contest of this protocol in June 2023.  
[https://github.com/sherlock-audit/2023-06-dodo/blob/a8d30e611acc9762029f8756d6a5b81825faf348/new-dodo-v3/contracts/DODOV3MM/D3Pool/D3Trading.sol#L190C32-L190C57](https://github.com/sherlock-audit/2023-06-dodo/blob/a8d30e611acc9762029f8756d6a5b81825faf348/new-dodo-v3/contracts/DODOV3MM/D3Pool/D3Trading.sol#L190C32-L190C57)

```solidity
   // previous contest snippet
   return (payFromAmount, receiveToAmount - swapFee, vusdAmount, swapFee, mtFee); //@audit returned value is receiveToAmount - swapFee 
```

It can be seen that the `swapFee` is deducted in the previous codebase. In that version, users would pay the swap fee but the maintainer fee was not users' responsibility. Maintainer fee was paid directly from the contract balance in the previous contest. I am not sure whether the new intention is making users to pay both swap fees and maintainer fees in this updated codebase but it is certain that the swap fee is not charged.

Also we can see how the [`_recordSwap()` is called](https://github.com/sherlock-audit/2023-06-dodo/blob/a8d30e611acc9762029f8756d6a5b81825faf348/new-dodo-v3/contracts/DODOV3MM/D3Pool/D3Trading.sol#L121C53-L121C78) in the previous contest. It is called with `receiveToAmount + swapFee`

```solidity
    // previous contest snippet
    _recordSwap(fromToken, toToken, vusdAmount, receiveToAmount + swapFee);
```

There are two consequences of this issue.

1. Users are not charged with swap fee and get more tokens than they should.
    
2. `_recordSwap` function is called with wrong value. _(it is [called using `swapFee`](https://github.com/sherlock-audit/2023-12-dodo/blob/main/dodo-v3/contracts/DODOV3MM/D3Pool/D3Trading.sol#L125C75-L125C100) like the previous codebase, but the actual value in this current codebase uses `mtFee`)._  There will be a mismatch between the record and the actual value if the `mtFee` and `swapFee` is not exactly the same.

## Impact

- Users don't pay swap fee and get more tokens
- `_recordSwap` function is called with incorrect `toAmount` parameter.

## Code Snippet
https://github.com/sherlock-audit/2023-12-dodo/blob/main/dodo-v3/contracts/DODOV3MM/D3Pool/D3Trading.sol#L206

https://github.com/sherlock-audit/2023-12-dodo/blob/main/dodo-v3/contracts/DODOV3MM/D3Pool/D3Trading.sol#L125C75-L125C100

## Tool used

Manual Review

## Recommendation
I am not sure whether the protocol wants users to pay both swap fees and maintainer fees. Depending on the protocol's intention with this new codebase, the returned value of how many tokens to receive in the `querySellTokens` function should be either `receiveToAmount - swapFee` or `receiveToAmount - swapFee - mtFee`
