Ambitious Holographic Cormorant

medium

# Replacing an old token can overwrite the price of an unintended active token

## Summary

This updated brings D3MakerFreeSlot which is a special type of D3Maker, in which the market maker can set a new token info into an existing token slot. This can be used when retiring a token in a market to save gas vs the original D3Maker implementation.

However the issue with the new implementation is that it doesn’t prohibit the same token slot from being updated more than once. This is concerning because it will result in multiple tokens sharing the same price slot which means that one of the tokens will be over/underpriced in the market, allowing users and probably math bots to profit significantly from trades. 

## Vulnerability Detail

When a market maker wants to set the price info for a token whilst also saving gas, they can do so by calling `setNewTokenAndReplace` in the new D3MakerFreeSlot contract.

```solidity
function setNewTokenAndReplace(
        address token,
        bool stableOrNot,
        uint80 priceSet,
        uint64 amountSet,
        uint16 kAsk,
        uint16 kBid,
        address oldToken
    ) external onlyOwner {
        require(state.priceListInfo.tokenIndexMap[token] == 0, Errors.HAVE_SET_TOKEN_INFO);
        require(state.priceListInfo.tokenIndexMap[oldToken] != 0, Errors.OLD_TOKEN_NOT_FOUND);
        // check amount
        require(kAsk >= 0 && kAsk <= 10000, Errors.K_LIMIT);
        require(kBid >= 0 && kBid <= 10000, Errors.K_LIMIT);

        for (uint256 i = 0; i < poolTokenlist.length; i++) {
            if (poolTokenlist[i] == oldToken) {
                poolTokenlist[i] = token;
                break;
            }
        }

        uint256 tokenIndex = uint256(getOneTokenOriginIndex(oldToken));
        bool isStable = (tokenIndex % 2 == 0);
        require(isStable == stableOrNot, Errors.STABLE_TYPE_NOT_MATCH);

        // remove old token info
        state.tokenMMInfoMap[oldToken].priceInfo = 0;
        state.tokenMMInfoMap[oldToken].amountInfo = 0;
        state.tokenMMInfoMap[oldToken].kAsk = 0;
        state.tokenMMInfoMap[oldToken].kBid = 0;
        state.tokenMMInfoMap[oldToken].tokenIndex = 0;

        // set new token info
        state.tokenMMInfoMap[token].priceInfo = priceSet;
        state.tokenMMInfoMap[token].amountInfo = amountSet;
        state.tokenMMInfoMap[token].kAsk = kAsk;
        state.tokenMMInfoMap[token].kBid = kBid;
        state.heartBeat.lastHeartBeat = block.timestamp;

        // set token price index
        if (stableOrNot) {
            // is stable
            uint256 indexInStable = tokenIndex / 2;
            uint256 innerSlotIndex = indexInStable % MakerTypes.PRICE_QUANTITY_IN_ONE_SLOT;
            uint256 slotIndex = indexInStable / MakerTypes.PRICE_QUANTITY_IN_ONE_SLOT;
            uint256 oldPriceSlot = state.priceListInfo.tokenPriceStable[slotIndex];
            uint256 newPriceSlot = stickPrice(oldPriceSlot, innerSlotIndex, priceSet);
            state.priceListInfo.tokenPriceStable[slotIndex] = newPriceSlot;
        } else {
            uint256 indexInNStable = (tokenIndex - 1) / 2;
            uint256 innerSlotIndex = indexInNStable % MakerTypes.PRICE_QUANTITY_IN_ONE_SLOT;
            uint256 slotIndex = indexInNStable / MakerTypes.PRICE_QUANTITY_IN_ONE_SLOT;
            uint256 oldPriceSlot = state.priceListInfo.tokenPriceNS[slotIndex];
            uint256 newPriceSlot = stickPrice(oldPriceSlot, innerSlotIndex, priceSet);
            state.priceListInfo.tokenPriceNS[slotIndex] = newPriceSlot;
        }
        // to avoid reset the same token, tokenIndexMap record index from 1, but actualIndex = tokenIndex[address] - 1
        state.priceListInfo.tokenIndexMap[token] = tokenIndex + 1;
        state.tokenMMInfoMap[token].tokenIndex = uint16(tokenIndex);

        emit SetNewToken(token);
        emit ReplaceToken(oldToken, token);
    }
```

The key point relevant to this report is that the `oldToken` needs to have already been added, which is validated by checking whether the index of the token price slot is non-zero ( `state.priceListInfo.tokenIndexMap[oldToken] != 0`). The token index to overwrite is then fetched with the call to `getOneTokenOriginIndex` which looks like:

```solidity
function getOneTokenOriginIndex(address token) public view returns (int256) {
        //require(state.priceListInfo.tokenIndexMap[token] > 0, Errors.INVALID_TOKEN);
        return int256(state.priceListInfo.tokenIndexMap[token]) - 1;
    }
```

For a token that has already been added, this will return a valid positive integer. This value is then used to compute the actual `slotIndex` that the new token price will be written into. This all works as intended for a normal new token addition.

However the issue with this implementation is that there is the potential for the market maker to replace an old token that has already been replaced before because the `tokenIndexMap` of the old token being replaced isn’t written to 0. This is especially likely if there is a significant time between token additions. For example, let’s say the market maker wanted to remove USDT and add USDC, they could call `setNewTokenAndReplace` to achieve this. Now, 6 months later the market maker wants to add WETH and they still don’t care about USDT so they call `setNewTokenAndReplace` with USDT as the `oldToken`.

This example transaction will complete successfully but it has the very unintended side effect of writing the price info of WETH over the top of the price info of USDC. Both USDC and WETH are pointing to the same part of storage and USDC is now completely mis-priced from what it should be.

## Impact

It is possible for a market maker’s new token addition to overwrite the price of another token inadvertently. This will result in the market price parameters of the first token to be incorrect and therefore allow users and bots to profit from any price discrepancies. It is also an unrecoverable situation that requires the D3MM pool to point to a new maker contract.

## Code Snippet

https://github.com/sherlock-audit/2023-12-dodo/blob/main/dodo-v3/contracts/DODOV3MM/D3PoolNoBorrow/D3MakerFreeSlot.sol#L21-L84

## Tool used

Manual Review

## Recommendation

The `state.priceListInfo.tokenIndexMap[oldToken]` storage slot should be set to 0 in the `setNewTokenAndReplace` method.
