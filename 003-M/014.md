Ancient Felt Gazelle

high

# Wrong calculation of `_recordSwap`'s `toAmount` when selling or buying tokens

## Summary

The calculation of `toAmount` passed into the `_recordSwap` function is incorrect in the `D3Trading.sellToken` and `D3MMNoBorrowed.sellToken` functions.

## Vulnerability Detail

The `receiveToAmount` represents the total amount of `to` tokens in the swap. The actual amount of `to` tokens that the user should receive is calculated in the `querySellTokens` function as `receiveToAmount - mtFee`.

However, in the `sellToken` function, when recording the swap, it requires the total amount of `to` tokens, including both `receiveToAmount` and `mtFee`. Unfortunately, it uses `receiveToAmount + swapFee`, which is incorrect.

```solidity
    function sellToken(
        address to,
        address fromToken,
        address toToken,
        uint256 fromAmount,
        uint256 minReceiveAmount,
        bytes calldata data
    ) external virtual poolOngoing nonReentrant returns (uint256) {
        require(ID3Maker(state._MAKER_).checkHeartbeat(), Errors.HEARTBEAT_CHECK_FAIL);

        _updateCumulative(fromToken);
        _updateCumulative(toToken);

        (uint256 payFromAmount, uint256 receiveToAmount, uint256 vusdAmount, uint256 swapFee, uint256 mtFee) =
            querySellTokens(fromToken, toToken, fromAmount);
        require(receiveToAmount >= minReceiveAmount, Errors.MINRES_NOT_ENOUGH);

        _transferOut(to, toToken, receiveToAmount);

        // external call & swap callback
        IDODOSwapCallback(msg.sender).d3MMSwapCallBack(fromToken, fromAmount, data);
        // transfer mtFee to maintainer
        if(mtFee > 0) {
            _transferOut(state._MAINTAINER_, toToken, mtFee);
        }

        require(
            IERC20(fromToken).balanceOf(address(this)) - state.balances[fromToken] >= fromAmount,
            Errors.FROMAMOUNT_NOT_ENOUGH
        );

        // record swap
        uint256 toTokenDec = IERC20Metadata(toToken).decimals();
//@audit -> toAmount should be receiveToAmount + mtFee instead of swapFee. 
        _recordSwap(fromToken, toToken, vusdAmount, Types.parseRealAmount(receiveToAmount + swapFee, toTokenDec));
        require(checkSafe(), Errors.BELOW_IM_RATIO);

        emit Swap(to, fromToken, toToken, payFromAmount, receiveToAmount, swapFee, mtFee, 0);
        return receiveToAmount;
    }
```

```solidity
    function querySellTokens(
        address fromToken,
        address toToken,
        uint256 fromAmount
    ) public view returns (uint256 payFromAmount, uint256 receiveToAmount, uint256 vusdAmount, uint256 swapFee, uint256 mtFee) {
        [....]
//@audit -> the returned receiveToAmount is (receiveToAmount - mtFee), so the toAmount should be (receiveToAmount + mtFee)
        return (payFromAmount, receiveToAmount - mtFee, vusdAmount, swapFee, mtFee);
    }
```

And the `toAmount` is directly added to the `cumulativeAsk` of `toToken`, which will lead to additional logic errors.

```solidity
    function _recordSwap(address fromToken, address toToken, uint256 fromAmount, uint256 toAmount) internal {
        tokenCumMap[fromToken].cumulativeBid += fromAmount;
        tokenCumMap[toToken].cumulativeAsk += toAmount;

        _updateReserve(fromToken);
        _updateReserve(toToken);
    }
```

## Impact

The `cumulativeAsk` for the `toToken` is completely incorrect, leading to an inaccurate `receiveToToken` value from `PMMPricing._querySellQuoteToken`. This can cause users to receive either fewer or more tokens than they should.

## Code Snippet

https://github.com/sherlock-audit/2023-12-dodo/blob/ea7f786161113144562a900dbff31457ff7025ef/dodo-v3/contracts/DODOV3MM/D3Pool/D3Trading.sol#L125
https://github.com/sherlock-audit/2023-12-dodo/blob/ea7f786161113144562a900dbff31457ff7025ef/dodo-v3/contracts/DODOV3MM/D3PoolNoBorrow/D3MMNoBorrow.sol#L82


## Tool used

Manual Review

## Recommendation
Use `receiveToAmount + mtFee` instead of `receiveToAmount + swapFee` to record the swap.

